#lang shplait

type Exp
| intE(n :: Int)
| idE(s :: Symbol)
| plusE(l :: Exp, r :: Exp)
| multE(l :: Exp, r :: Exp)
| appE(s :: Symbol, arg :: Exp)
| letE(s :: Symbol, rhs :: Exp, body :: Exp)

type FunDef
| fd(name :: Symbol, arg :: Symbol, body :: Exp)

type Binding
| bind(name :: Symbol, val :: Int)

type Env = Listof(Binding)

def mt_env = []
def extend_env = cons


fun find(l :: Listof(?A), f :: ?A -> Boolean) :: Optionof(?A):
  fun find_inner(l :: Listof(?A)) :: Optionof(?A):
    match l
    | []: none()
    | cons(x, xs):
        if f(x):
        | some(x)
        | find_inner(xs)
  find_inner(l)

fun lookup(n :: Symbol, env :: Env):
  match find(env, fun(x): bind.name(x) == n)
  | some(x): x
  | none(): error(#'lookup, "free variable: " +& n)

fun get_fundef(s :: Symbol, funs :: Listof(FunDef)) :: FunDef:
  match find(funs, fun(x): fd.name(x) == s)
  | some(x): x
  | none(): error(#'get_fundef, "function not found: " +& s)


fun subst(what :: Exp, for :: Symbol, in :: Exp):
  match in:
  | idE(x):
      if x == for
      | what
      | in
  | plusE(l, r): plusE(subst(what, for, l), subst(what, for, r))
  | multE(l, r): multE(subst(what, for, l), subst(what, for, r))
  | appE(s, arg): appE(s, subst(what, for, arg))
  | ~else:
      in

fun interp(expr :: Exp, env :: Env, funs :: Listof(FunDef)) :: Int:
  fun interp_in1(expr :: Exp, env :: Env):
    fun interp_in2(expr :: Exp):
      match expr
      | intE(val): val
      | plusE(l, r): interp_in2(l) + interp_in2(r)
      | multE(l, r): interp_in2(l) * interp_in2(r)
      | idE(name): bind.val(lookup(name, env))
      | appE(fname, aarg):
          def fundef = get_fundef(fname, funs)
          def farg = fd.arg(fundef)
          def fbody = fd.body(fundef)
          interp_in1(fbody, [bind(farg, interp_in2(aarg))])
      | letE(sym, rhs, body):
          interp_in1(body, extend_env(bind(sym, interp_in2(rhs)), env))
    interp_in2(expr)
  interp_in1(expr, env)

fun parse(s :: Syntax) :: Exp:
  cond
  | syntax_is_integer(s):
      intE(syntax_to_integer(s))
  | syntax_is_symbol(s):
      idE(syntax_to_symbol(s))
  | ~else:
      match s
      | 'let $sym = $rhs: $body': letE(syntax_to_symbol(sym), parse(rhs), parse(body))
      | '$left + $right': plusE(parse(left), parse(right))
      | '$left * $right': multE(parse(left), parse(right))
      | '$sym($arg)': appE(syntax_to_symbol(sym),parse(arg))
      | '($inner)': parse(inner)
      | ~else: error(#'parse, "bad syntax: " +& s)

fun parse_fundef(s :: Syntax):
    match s:
    | 'fun $fname($argname): $body':
        fd(syntax_to_symbol(fname), syntax_to_symbol(argname), parse(body))



module test:

  def bad = parse_fundef('fun bad(x): y')
  def double = parse_fundef('fun double(x): x + x')
  def quadruple = parse_fundef('fun quadruple(x): double(double(x))')
  def funs = [double, quadruple, bad]
  
  // Interpreter tests
  check: interp(intE(42), mt_env, [])
         ~is 42

  check: interp(plusE(intE(9), intE(10)), mt_env, [])
         ~is 19

  check: interp(plusE(multE(intE(4), intE(5)), intE(6)), mt_env, [])
         ~is 26

  check: interp(parse('x + 4'), [bind(#'x, 4)], [])
         ~is 8

  check: interp(parse('x'), mt_env, [])
         ~raises "free variable"

  check: interp(parse('quadruple(2)'), mt_env, funs)
         ~is 8

  check: interp(parse('let x = 5 : x + x'), mt_env, funs)
         ~is 10

  check: interp(parse('let x = 5 * quadruple(7) : let y = double(x) + x : x + y'), mt_env, funs)
         ~is 560

  // Expression parser tests
  check: parse('1')
         ~is intE(1)

  check: parse('1 + 2')
         ~is plusE(intE(1), intE(2))
       
  check: parse('1 * 2')
         ~is multE(intE(1), intE(2))

  check: parse('1 + 2 * 3 + 4')
         ~is plusE(plusE(intE(1), multE(intE(2), intE(3))), intE(4))

  check: parse('1 * (2 + 3)')
         ~is multE(intE(1), plusE(intE(2), intE(3)))

  check: parse('x')
         ~is idE(#'x)

  check: parse('double(9)')
         ~is appE(#'double, intE(9))

  check: parse('1 + double(9)')
         ~is plusE(intE(1), appE(#'double, intE(9)))

  check: parse('1 + + 2')
         ~raises "bad syntax"

  // Function parser tests

  check: parse_fundef('fun double(x): x + x')
         ~is fd(#'double, #'x, plusE(idE(#'x), idE(#'x)))

  // Function lookup tests

  check: get_fundef(#'double, funs)
         ~is double

  check: get_fundef(#'quadruple, funs)
         ~is quadruple

  check: get_fundef(#'triple, funs)
         ~raises "function not found"

  // Substitution tests
  check: subst(parse('8'), #'x, parse('9'))
         ~is parse('9')
  check: subst(parse('8'), #'x, parse('x'))
         ~is parse('8')
  check: subst(parse('8'), #'x, parse('y'))
         ~is parse('y')
  check: subst(parse('8'), #'x, parse('x + y'))
         ~is parse('8 + y')
  check: subst(parse('8'), #'x, parse('x * y'))
         ~is parse('8 * y')
  check: subst(parse('8'), #'x, parse('double(x)'))
         ~is parse('double(8)')
